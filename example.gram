module {
	val Types = module {
		val Eq = item => typeof(item)<(any=>any==item )>;
		val Neq= item => typeof(item)<(any=>!any==item)>;
		val GT = item => typeof(item)<(any=>any>item  )>;
		val LT = item => typeof(item)<(any=>any<item  )>;
	};

	val Math = module {
		val PositiveInt = Types::GT(-1);
		val NegativeInt = Types::LT( 1);

		val Square: Int->Int = n=>n*n;

		val Abs: Int->PositiveInt = n=> n match {
			n: NegativeInt => -n;
			n: PositiveInt =>  n;
		};

		val Pow: {Int; Int}->Int = args=> args match {
			{n; exponent: Types::Eq 0 } => 1;
			{n; exponent: PositiveInt } => n * (Pow{n; exponent-1});
		};

		val Max = list=>reduce{Max';list} 
			where val Max': {Int; Int}->Int = {a;b} => if (a<b) b else a;

		val Min = list=>reduce{Min';list} 
			where val Min': {Int; Int}->Int = {a;b} => if (a<b) a else b;

		val Pi: Int = 3;
	};

	val Rng = args => args match {
			{}			=> mkRng 5; #{5 is the randomly chosen integer providing this RNG's entropy}#
			seed: Int	=> mkRng seed;
		}
		where val mkRng = s => module {
									var seed = s;

									val Next: {}->Int = {}=>seed 
										where {
											seed = (Math::Abs((seed * a + c) % m))
												where {
													val a = 1103515245;
													val c = 12345;
													val m = Math::Pow{2;30};
												}};
								};

	val Bool = Int<bool=> bool == 0 || bool == 1>;
	val true: Bool = 1;
	val false: Bool = 0;

	val range = p:{Int;Int} => range'(p+{p[0]})
		where val range' = {current:Int; end:Int; rangeList} => 
			if (current == end) 
				rangeList 
			else 
				range'({current + 1; end; rangeList + (current+1)});

	var head = {head;_...}=>head;

	var tail = {_;tail...}=>tail;

	val map = {f; iter} => for(i:iter) f(i);

	val filter: {Any->Any; Any} -> Any = args=>filter'(args + {})
		where val filter' = {f; iter; current} => 
			if (length(iter) == 0) 
				current 
			else if (f(head(iter))) 
				filter'{f; tail iter; current + (head(iter))} 
			else 
				filter'{f; tail iter; current};

	val listConcat = args=>reduce{concat'; args} 
		where val concat' = {l1; l2}=>l2 match {
				{} 				=> l1;
				{head; tail...} => concat'{l1 + head; tail}
			};

	val reduce = {f; iter}=>iter match {
		{head}				=> head;
		{one; two}			=> f{one;two};
		{one; two; rest...}	=> reduce{f; 
									listConcat{
										{f{one; two}}; 
										rest
									}
								}
	};

	var repeat = {f;n}=>n match{
		n: Types::Eq 0	=> {};
		n: Types::Eq 1	=> {f{}};	
		n				=> listConcat{{f{}}; repeat{f;n-1}};
	};
};